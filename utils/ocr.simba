(*
# OCR
This page is about WaspLib's OCR.
*)

{$DEFINE WL_OCR_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}

type
  TOCR = record
    Engine: TPixelOCR;
  end;

function TOCR.RecognizeStatic(bounds: TBox; font: TPixelFont; colors: TColorArray; tolerance: Single): String;
var
  img: TImage;
begin
  img := Target.GetImage(bounds);
  img.ReplaceColorBinary(False, colors, tolerance);
  Result := Self.Engine.Recognize(img, font, [0,0]);
end;

function TOCR.RecognizeStaticInvert(bounds: TBox; font: TPixelFont; colors: TColorArray): String;
var
  img: TImage;
begin
  img := Target.GetImage(bounds);
  img.ReplaceColorBinary(True, colors);
  Result := Self.Engine.Recognize(img, font, [0,0]);
end;

function TOCR.Recognize(bounds: TBox; font: TPixelFont; colors: TColorArray; tolerance: Single): String;
var
  img: TImage;
begin
  img := Target.GetImage(bounds);
  img.ReplaceColorBinary(False, colors, tolerance);
  Result := Self.Engine.Recognize(img, Font);
end;

function TOCR.RecognizeLines(bounds: TBox; font: TPixelFont; colors: TColorArray; tolerance: Single): TStringArray;
var
  img: TImage;
begin
  img := Target.GetImage(bounds);
  img.ReplaceColorBinary(False, colors, tolerance);
  Result := Self.Engine.RecognizeLines(img, font);
end;

function TOCR.RecognizeShadow(bounds: TBox; font: TPixelFont; tolerance: Single): String;
var
  img: TImage;
  shadows: TPointArray;
  colors: TColorArray;
begin
  img := Target.GetImage(bounds);
  shadows := img.FindColor($0, 2.5, [1, 1, img.Width - 1, img.Height - 1]);
  shadows := shadows.Offset([-1,-1]).ExcludePoints(shadows);

  colors := img.GetColors(shadows).Unique();
  img.ReplaceColorBinary(False, colors, tolerance);

  Result := Self.Engine.Recognize(img, font);
end;

function TOCR.RecognizeNumber(bounds: TBox; font: TPixelFont; colors: TColorArray; tolerance: Single): Integer;
begin
  Result := Self.Recognize(bounds, font, colors, tolerance).ExtractInteger();
end;

function TOCR.Locate(bounds: TBox; Text: String; colors: TColorArray; tolerance: Single; font: TPixelFont): Single;
var
  img: TImage;
begin
  img := Target.GetImage(bounds);
  img.ReplaceColorBinary(False, colors, tolerance);
  Result := Self.Engine.Locate(img, font, Text);
end;

function TOCR.LocateInvert(bounds: TBox; Text: String; colors: TColorArray; tolerance: Single; font: TPixelFont): Single;
var
  img: TImage;
begin
  img := Target.GetImage(bounds);
  img.ReplaceColorBinary(True, colors, tolerance);
  Result := Self.Engine.Locate(img, font, Text);
end;

var
  OCR: TOCR;

type
  TRSFonts = record
    PLAIN_11:       TPixelFont;
    PLAIN_12:       TPixelFont;
    BOLD:           TPixelFont;
    BOLD_SHADOW:    TPixelFont;
    QUILL_8:        TPixelFont;
    QUILL:          TPixelFont;
    Unzipping: Boolean;
    Timeout: TCountDown;

    const WHITE: TColor        = $FFFFFF;
    const BLACK: TColor        = $000000;
    const ORANGE: TColor       = $1F98FF;
    const GREY: TColor         = $9F9F9F;
    const GREEN: TColor        = $00FF00;
    const LIGHT_GREEN: TColor  = $0DC10D;
    const YELLOW: TColor       = $00FFFF;
    const RED: TColor          = $0000FF;
    const BLUE: TColor         = $FF0000;
    const DARK_BLUE: TColor    = $800000;
    const LIGHT_ORANGE: TColor = $3FB8FF;
    const LIGHT_YELLOW: TColor = $A2D8E4;
    const BROWN: TColor        = $002040;
    const PATH = WLEnv.CacheDir + 'fonts' + PATH_SEP;
  end;

procedure TRSFonts.Load();
begin
  Self.PLAIN_11 := OCR.Engine.LoadFont(Self.PATH + 'Plain 11', 4);
  Self.PLAIN_12 := OCR.Engine.LoadFont(Self.PATH + 'Plain 12', 4);

  Self.BOLD        := OCR.Engine.LoadFont(Self.PATH + 'Bold 12', 4);
  Self.BOLD_SHADOW := OCR.Engine.LoadFont(Self.PATH + 'Bold 12 Shadow', 4);

  Self.QUILL_8 := OCR.Engine.LoadFont(Self.PATH + 'Quill 8', 4);
  Self.QUILL   := OCR.Engine.LoadFont(Self.PATH + 'Quill', 4);
end;

procedure TRSFonts._UnzipFinished(constref result: TASyncUnzipResult);
begin
  if not result.Success then
  begin
    DirDelete(Self.PATH, True);
    raise GetDebugLn('RSFonts', 'Failed to unzip fonts: '  + result.Exception);
  end;

  Self.Load();
  Self.Unzipping := False;
end;

procedure TRSFonts.WaitUnzipping();
begin
  while Self.Unzipping do
  begin
    Sleep(100);
    if not Self.Timeout.IsFinished then
      Continue;
    DirDelete(Self.PATH, True);
    raise GetDebugLn('RSFonts', 'Timed out unzipping fonts.');
  end;
end;

procedure TRSFonts.Setup();
begin
  if DirList(Self.PATH) <> [] then
  begin
    Self.Load();
    Exit;
  end;

  if not DirCreate(Self.PATH) then
    raise GetDebugLn('OCR', 'Failed to create path: ' + Self.PATH);

  Self.Unzipping := True;
  Self.Timeout.Start(60 * ONE_SECOND);
  Async.FileUnZip(WLEnv.AssetsDir + 'fonts' + PATH_SEP + 'fonts.zip', Self.PATH, @Self._UnzipFinished);
end;

var
  RSFonts: TRSFonts;
