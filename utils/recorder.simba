(*
# Recorder
Records video in separate thread for debugging script crashes and issues.

Basic usage:
```pascal
SimbaRecorder.Start(30); // Start recording with 30 second buffer
SimbaRecorder.Start(30, 'MyVideos'); // Custom save directory
// Videos are automatically saved on script end
```
*)

{$DEFINE WL_RECORDER_INCLUDED}
{$INCLUDE_ONCE WaspLib/utils.simba}

{$loadlib Plugins/wasp-plugins/librecorder/librecorder}

type
(*
## type TSimbaRecorder
```pascal
TSimbaRecorder = record
  Thread: TThread;
  IsRecording: Boolean;
  BufferSeconds: Integer;
  VideoPath: String;
  RecorderTarget: TTarget;
end;
```
Manages video recording with buffered capture.
*)
  TSimbaRecorder = record
    Thread: TThread;
    Enabled, IsRecording: Boolean;
    BufferSeconds: Integer;
    VideoPath: String;
    CurrentFrame: TImage;
    RecorderTarget: TTarget;
    FrameCount: Integer;
  end;

var
  SimbaRecorder: TSimbaRecorder;

 // Captures frame from target
{$H-}
function SimbaRecorder_GetFrame(sender: TRecorder): Pointer;
begin
  if Assigned(SimbaRecorder.CurrentFrame) then
    SimbaRecorder.CurrentFrame.Destroy();

  SimbaRecorder.CurrentFrame := SimbaRecorder.RecorderTarget.GetImage();
  Inc(SimbaRecorder.FrameCount);

  Result := SimbaRecorder.CurrentFrame.Data;
end;

// Checks if recording should stop
function SimbaRecorder_GetTerminated(sender: TRecorder): Boolean;
begin
  Result := not SimbaRecorder.IsRecording;
end;
{$H+}

// Cleanup on script termination
procedure TSimbaRecorder.Stop();
begin
  SimbaRecorder.IsRecording := False;
  if Assigned(SimbaRecorder.Thread) then
  begin
    SimbaRecorder.Thread.WaitForTerminate();
    SimbaRecorder.Thread.Free();
    SimbaRecorder.Thread := nil;
  end;

  SimbaRecorder.RecorderTarget := nil;
end;

// Thread execution handler
procedure TSimbaRecorder._Execute(); 
var
  recorder: TRecorder;
begin
  try
    recorder := TRecorder.Create(
                  Self.BufferSeconds, Self.VideoPath,
                  Self.RecorderTarget.Width, Self.RecorderTarget.Height,
                  @SimbaRecorder_GetFrame,
                  @SimbaRecorder_GetTerminated
                );
    // Fixed 15 FPS recording rate (DLL limitation)
    recorder.SetFFMPEG(SimbaEnv.PluginsPath + 'wasp-plugins' + PATH_SEP + 'librecorder' + PATH_SEP + 'ffmpeg.exe');
    recorder.Run(False);
  except
    WriteLn('[Recorder] Error: ', GetExceptionMessage());
  finally
    recorder.Free();
  end;
end;

(*
## TSimbaRecorder.Start
```pascal
procedure TSimbaRecorder.Start(BufferSeconds: Integer = 30; VideoPath: String = 'Videos');
```
Starts recording with specified buffer duration and save path.
*)
procedure TSimbaRecorder.Start(bufferSeconds: Integer = 0);
begin
  if Self.IsRecording then
    Exit;

  if bufferSeconds <> 0 then
    Self.BufferSeconds := bufferSeconds;
  if Self.VideoPath <> '' then
    Self.VideoPath := WLEnv.VideosDir;
  Self.IsRecording := True;
  
  // Create separate target for recorder thread
  Self.RecorderTarget := new TTarget();
  Self.RecorderTarget.SetWindow(GetSimbaTargetWindow());
  
  Self.Thread := TThread.Create(@Self._Execute);
  Self.Thread.Name := 'RecorderThread_' + IntToStr(Time());

  AddOnTerminate(@SimbaRecorder.Stop);
end;
