(*
# Combat
Methods to interact with the combat gametab:
```{figure} ../../images/combattab.png
```
*)
{$DEFINE WL_COMBAT_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

const
  WEAPON_CATEGORIES: TStringArray = [
    'Axe', 'Banner', 'Blunt', 'Bludgeon', 'Bulwark', 'Claw', 'Egg', 'Partisan',
    'Pickaxe', 'Polearm', 'Polestaff', 'Scythe', 'Slash Sword', 'Spear',
    'Spiked', 'Stab Sword', '2H Sword', 'Unarmed', 'Whip',
    'Blaster', 'Bow', 'Chinchompa', 'Crossbow', 'Gun', 'Thrown',
    'Bladed Staff', 'Powered Staff', 'Staff', 'Salamander'
  ];

  WEAPON_STYLES: TStringArray = [
    'Bash', 'Pound', 'Pummel', 'Spike', 'Impale', 'Focus', 'Chop', 'Hack',
    'Stab', 'Slash', 'Lunge', 'Block', 'Flick', 'Lash', 'Deflect', 'Swipe',
    'Jab', 'Fend', 'Smash', 'Punch', 'Kick', 'Scorch',
    'Accurate', 'Rapid', 'Longrange', 'Flare',
    'Spell', 'Blaze'
  ];

type
  EWeaponCategory = enum(
    //Melee
    AXE, BANNER, BLUNT, BLUDGEON, BULWARK, CLAW, EGG, PARTISAN, PICKAXE,
    POLEARM, POLESTAFF, SCYTHE, SLASH_SWORD, SPEAR, SPIKED, STAB_SWORD,
    TWO_HANDED_SWORD, UNARMED, WHIP,
    //Ranged
    BLASTER, BOW, CHINCHOMPA, CROSSBOW, GUN, THROWN,
    //Magic
    BLADED_STAFF, POWERED_STAFF, STAFF,
    //Other
    SALAMANDER
  );

  EMeleeStyle = enum(NONE, STAB, SLASH, CRUSH);

  EWeaponStyle = enum(
    BASH, POUND, PUMMEL, SPIKE, IMPALE, FOCUS, CHOP, HACK, STAB, SLASH, LUNGE,
    BLOCK, FLICK, LASH, DEFLECT, SWIPE, JAB, FEND, SMASH, PUNCH, KICK, SCORCH,
    ACCURATE, RAPID, LONGRANGE, FLARE,
    SPELL, BLAZE
  );

function EWeaponStyle.GetMeleeStyle(category: EWeaponCategory): EMeleeStyle;
begin
  case Self of
    EWeaponStyle.SPIKE, EWeaponStyle.IMPALE, EWeaponStyle.STAB, EWeaponStyle.LUNGE:
      Result := EMeleeStyle.STAB;

    EWeaponStyle.CHOP, EWeaponStyle.HACK, EWeaponStyle.SLASH,
    EWeaponStyle.FLICK, EWeaponStyle.LASH, EWeaponStyle.DEFLECT,
    EWeaponStyle.SWIPE, EWeaponStyle.SCORCH:
      Result := EMeleeStyle.SLASH;

    EWeaponStyle.BASH, EWeaponStyle.POUND, EWeaponStyle.PUMMEL,
    EWeaponStyle.FOCUS, EWeaponStyle.SMASH, EWeaponStyle.PUNCH, EWeaponStyle.KICK:
      Result := EMeleeStyle.CRUSH;

    EWeaponStyle.BLOCK:
    begin
      case category of
        EWeaponCategory.AXE, EWeaponCategory.CLAW,
        EWeaponCategory.SCYTHE, EWeaponCategory.SLASH_SWORD,
        EWeaponCategory.TWO_HANDED_SWORD:
          Result := EMeleeStyle.SLASH;

        EWeaponCategory.BANNER, EWeaponCategory.PARTISAN,
        EWeaponCategory.PICKAXE, EWeaponCategory.SPEAR, EWeaponCategory.STAB_SWORD:
          Result := EMeleeStyle.STAB;

        EWeaponCategory.BLUNT, EWeaponCategory.EGG, EWeaponCategory.POLESTAFF,
        EWeaponCategory.SPIKED, EWeaponCategory.UNARMED:
          Result := EMeleeStyle.CRUSH;
      end;
    end;

    EWeaponStyle.JAB:
    begin
      case category of
        EWeaponCategory.POLEARM, EWeaponCategory.BLADED_STAFF:
          Result := EMeleeStyle.STAB;
        EWeaponCategory.SCYTHE:
          Result := EMeleeStyle.CRUSH;
      end;
    end;

    EWeaponStyle.FEND:
    begin
      case category of
        EWeaponCategory.POLEARM: Result := EMeleeStyle.STAB;
        EWeaponCategory.BLADED_STAFF: Result := EMeleeStyle.CRUSH;
      end;
    end;
  end;
end;

type
(*
## TRSCombat
Main record responsible with handling the combat gametab.
*)
  TRSCombat = record
    AutoRetaliateBox, SpecialAttackBox,
    CategoryBox, _StylesBox: TBox;
    _StyleBoxes: array [0..2] of TBoxArray;
  end;

procedure TRSCombat.SetupGameTab();
begin
  with GameTab.TopLeft do
  begin
    Self.AutoRetaliateBox.X1 := X+18;
    Self.AutoRetaliateBox.Y1 := Y+155;
    Self.AutoRetaliateBox.X2 := X+165;
    Self.AutoRetaliateBox.Y2 := Y+196;

    Self.SpecialAttackBox.X1 := X+18;
    Self.SpecialAttackBox.Y1 := Y+206;
    Self.SpecialAttackBox.X2 := X+165;
    Self.SpecialAttackBox.Y2 := Y+229;

    Self.CategoryBox.X1 := X+22;
    Self.CategoryBox.Y1 := Y+235;
    Self.CategoryBox.X2 := X+161;
    Self.CategoryBox.Y2 := Y+255;

    Self._StylesBox.X1 := X+18;
    Self._StylesBox.Y1 := Y+47;
    Self._StylesBox.X2 := X+165;
    Self._StylesBox.Y2 := Y+148;

    Self._StyleBoxes[0] := TBoxArray.Create([X+18,Y+47], 2, 2, 68, 44, [11, 10]);
    Self._StyleBoxes[1] := Copy(Self._StyleBoxes[0], 0, 3);
    Self._StyleBoxes[2] := TBoxArray.Create([X+18,Y+47], 1, 3, 68, 29, [0, 7]);
    Self._StyleBoxes[2] += TBoxArray.Create([X+97,Y+47], 1, 2, 68, 47, [0, 7]);
  end;
end;


(*
## Combat.IsOpen
```pascal
function TRSCombat.IsOpen(): Boolean;
```
Returns true/false if the combat tab is open.

Example:
```pascal
WriteLn Combat.IsOpen();
```
*)
function TRSCombat.IsOpen(): Boolean;
begin
  Result := GameTabs.IsOpen(ERSGameTab.COMBAT);
end;

(*
## Combat.Open
```pascal
function TRSCombat.Open(): Boolean;
```
Attempts to open the combat gametab.

Example:
```pascal
WriteLn Combat.Open();
```
*)
function TRSCombat.Open(): Boolean;
begin
  Result := GameTabs.Open(ERSGameTab.COMBAT);
end;


(*
## Combat.GetCategory
```pascal
function TRSCombat.GetCategory(): EWeaponCategory;
```
Returns the `EWeaponCategory` of the current weapon.

Example:
```pascal
{$I WaspLib/osrs.simba}
begin
  WriteLn Combat.GetCategory();
end.
```
*)
function TRSCombat.GetCategory(): EWeaponCategory;
var
  str: String;
begin
  str := OCR.Recognize(Self.CategoryBox, RSFonts.PLAIN_11, [RSFonts.ORANGE], 0);
  str := str.After('Category: ');

  Result := EWeaponCategory(WEAPON_CATEGORIES.IndexOf(str));
end;

(*
## Combat.StyleBoxes
```pascal
property TRSCombat.StyleBoxes: TBoxArray;
```
Returns the the attack style boxes available.

Example:
```pascal
{$I WaspLib/osrs.simba}
begin
  while True do
    ShowOnTarget(Combat.StyleBoxes);
end.
```
```{figure} ../../images/attackstyles.gif
```
*)
property TRSCombat.StyleBoxes: TBoxArray;
var
  tpa: TPointArray;
begin
  tpa := Target.FindColor(RSFonts.ORANGE, 0, Self._StylesBox);

  case Length(tpa.Cluster(6,6)) of
    3: Exit(Self._StyleBoxes[1]);
    4: Exit(Self._StyleBoxes[0]);
    5: Exit(Self._StyleBoxes[2]);
  end;
end;


function TRSCombat._GetWeaponStyle(b: TBox): EWeaponStyle;
var
  str: String;
begin
  str := OCR.Recognize(b, RSFonts.PLAIN_11, [RSFonts.ORANGE], 0);
  Result := EWeaponStyle(WEAPON_STYLES.IndexOf(str));
end;


(*
## Combat.StyleBoxes
```pascal
property TRSCombat.WeaponStyle: EWeaponStyle;
property TRSCombat.WeaponStyle(value: EWeaponStyle): Boolean;
```
Gets/Sets the weapon `EWeaponStyle`.

Example:
```pascal
{$I WaspLib/osrs.simba}
begin
  WriteLn Combat.WeaponStyle;
  WriteLn Combat.WeaponStyle := EWeaponStyle.BLOCK;
  WriteLn Combat.WeaponStyle;
end.
```
*)
property TRSCombat.WeaponStyle: EWeaponStyle;
var
  b: TBox;
begin
  for b in Self.StyleBoxes do
    if Target.HasColor([$1D1F82, 0.560, EColorSpace.HSL, [1.403, 1.403, 0.196]], 1, b) then
      Exit(Self._GetWeaponStyle(b));
end;

property TRSCombat.WeaponStyle(value: EWeaponStyle): Boolean;
var
  b: TBox;
  str: String;
begin
  if not Self.Open() then
    Exit;

  for b in Self.StyleBoxes do
  begin
    str := OCR.Recognize(b, RSFonts.PLAIN_11, [RSFonts.ORANGE], 0);
    if str <> WEAPON_STYLES[value] then Continue;

    if Target.HasColor([$1D1F82, 0.560, EColorSpace.HSL, [1.403, 1.403, 0.196]], 1, b) then
      Exit(True);

    Mouse.Click(b, EMouseButton.LEFT);
    Exit(
      SleepUntil(
        Target.HasColor([$1D1F82, 0.560, EColorSpace.HSL, [1.403, 1.403, 0.196]], 1, b),
        200, 3*TICK
      )
    );
  end;
end;


function TRSCombat._GetMeleeStyle(category: EWeaponCategory): EMeleeStyle;
var
  weaponStyle: EWeaponStyle;
begin
  weaponStyle := Self.WeaponStyle;
  Result := weaponStyle.GetMeleeStyle(category);
end;


(*
## Combat.StyleBoxes
```pascal
property TRSCombat.MeleeStyle: EMeleeStyle;
property TRSCombat.MeleeStyle(value: EMeleeStyle): Boolean;
```
Gets/Sets the combat `EMeleeStyle`. Doesn't do anything if we don't have a
melee weapon.

Example:
```pascal
{$I WaspLib/osrs.simba}
begin
  WriteLn Combat.MeleeStyle;
  WriteLn Combat.MeleeStyle := EMeleeStyle.SLASH;
  WriteLn Combat.MeleeStyle;
end.
```
*)
property TRSCombat.MeleeStyle: EMeleeStyle;
begin
  Result := Self._GetMeleeStyle(Self.GetCategory());
end;

property TRSCombat.MeleeStyle(value: EMeleeStyle): Boolean;
var
  weaponStyle: EWeaponStyle;
  category: EWeaponCategory;
  b: TBox;
begin
  if not Self.Open() then
    Exit;

  category := Self.GetCategory();
  if value = Self._GetMeleeStyle(category) then
    Exit(True);

  for b in Self.StyleBoxes do
  begin
    weaponStyle := Self._GetWeaponStyle(b);
    if weaponStyle.GetMeleeStyle(category) <> value then
      Continue;
    Mouse.Click(b, EMouseButton.LEFT);

    Exit(
      SleepUntil(
        Target.HasColor([$1D1F82, 0.560, EColorSpace.HSL, [1.403, 1.403, 0.196]], 1, b),
        200, 3*TICK
      )
    );
  end;
end;


(*
## Combat.AutoRetaliate
```pascal
property TRSCombat.AutoRetaliate: Boolean;
property TRSCombat.AutoRetaliate(value: Boolean): Boolean;
```
Gets/Sets the auto retaliate state.

Example:
```pascal
{$I WaspLib/osrs.simba}
begin
  WriteLn Combat.AutoRetaliate;
  Combat.AutoRetaliate := True;
  WriteLn Combat.AutoRetaliate;
  Combat.AutoRetaliate := False;
  WriteLn Combat.AutoRetaliate;
end.
```
*)
property TRSCombat.AutoRetaliate: Boolean;
begin
  Result := Target.HasColor([$1D1F82, 0.560, EColorSpace.HSL, [1.403, 1.403, 0.196]], 1, Self.AutoRetaliateBox);
end;

property TRSCombat.AutoRetaliate(value: Boolean): Boolean;
begin
  if not Self.Open() then
    Exit;
  if Self.AutoRetaliate = value then
    Exit(True);

  Mouse.Click(Self.AutoRetaliateBox, EMouseButton.LEFT);
  Result := SleepUntil(Self.AutoRetaliate = value, 200, 3 * TICK);
end;


(*
## Combat.SpecialAttack
```pascal
property TRSCombat.SpecialAttack: Boolean;
property TRSCombat.SpecialAttack(value: Boolean): Boolean;
```
Gets/Sets the special attack state.

Example:
```pascal
{$I WaspLib/osrs.simba}
begin
  WriteLn Combat.SpecialAttack;
  Combat.SpecialAttack := True;
  WriteLn Combat.SpecialAttack;
  Combat.SpecialAttack := False;
  WriteLn Combat.SpecialAttack;
end.
```
*)
property TRSCombat.SpecialAttack: Boolean;
begin
  Result := Minimap.SpecialEnabled();
end;

property TRSCombat.SpecialAttack(value: Boolean): Boolean;
begin
  if not Minimap.HasSpecial() then
    Exit;

  if Self.SpecialAttack = value then
    Exit(True);

  if not Self.IsOpen() then
  begin
    if value then
      Exit(Minimap.EnableSpecial(0));
    Exit(Minimap.DisableSpecial());
  end;

  Mouse.Click(Self.SpecialAttackBox, EMouseButton.LEFT);
  Result := SleepUntil(Self.SpecialAttack = value, 200, 3 * TICK);
end;

var
(*
## Combat variable
Global {ref}`TRSCombat` variable.
*)
  Combat: TRSCombat;
