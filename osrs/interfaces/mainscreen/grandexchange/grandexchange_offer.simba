(*
# GrandExchangeOffer
Methods to interact with the GrandExchangeOffer interface:
```{figure} ../../images/geo_interface.png
```

The {ref}`GrandExchange` is a very complex interface, probably the most complex
available in OldSchool RuneScape and for that reason, the functionality to
fully interact with it is split into multiple files and records:
- {ref}`GrandExchange`, the main grand exchange screen.
- {ref}`GrandExchangeHistory`, the history screen of the grand exchange.
- {ref}`GrandExchangeChat`, the chat interface of the grand exchange when you are setting up a buy offer.
- {ref}`GrandExchangeOffer`, the screen of the grand exchange where you view or setup a new offer and what this page is about.
*)

{$DEFINE WL_GRANDEXCHANGE_OFFER_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
## EGEOfferInterface
```pascal
EGEOfferInterface = enum(SETUP, STATUS);
```
Enum to represent the type of {ref}`GrandExchange` offer interfaces.

There's 2 types of "Offer" interfaces:
```{figure} ../../images/geo_setup.png
The "Setup offer" interface.
```

```{figure} ../../images/geo_status.png
The "Status offer" interface.
```

You can differ them by their layout and interface title.
*)
  EGEOfferInterface = enum(SETUP, STATUS);

(*
## EGEOfferType
```pascal
EGEOfferType = enum(SELL, BUY);
```
Enum to represent the type of Grand Exchange offers.
*)
  EGEOfferType = enum(SELL, BUY);

(*
## EGEOfferSpinButton
```pascal
EGEOfferSpinButton = enum(DECREASE, INCREASE);
```
Enum that represents the buttons to decrease/increase the quantity and price
spinners of Grand Exchange offers.

```{figure} ../../images/geo_spinners.png
```
*)
  EGEOfferSpinButton = enum(DECREASE, INCREASE);

(*
## EGEOfferQuantity
```pascal
EGEOfferQuantity = enum(ONE, TEN, HUNDRED, THOUSAND, CUSTOM);
```
Enum that represents the quantity buttons of Grand Exchange offers.

```{figure} ../../images/geo_quantity_buttons.png
```
*)
  EGEOfferQuantity = enum(ONE, TEN, HUNDRED, THOUSAND, CUSTOM);

(*
## EGEOfferPrice
```pascal
EGEOfferPrice = enum(MINUS_CUSTOM, MINUS_FIVE, GUIDE, CUSTOM, PLUS_FIVE, PLUS_CUSTOM);
```
Enum that represents the price buttons of Grand Exchange offers.

```{figure} ../../images/geo_price_buttons.png
```
*)
  EGEOfferPrice = enum(MINUS_CUSTOM, MINUS_FIVE, GUIDE, CUSTOM, PLUS_FIVE, PLUS_CUSTOM);

(*
## TRSGrandExchangeOffer
Record responsible to handle the {ref}`GrandExchangeOffer` interface.
*)
  TRSGrandExchangeOffer = record
    Title: TRSInterfaceTitle;
    Bounds: TBox;

    InfoBounds: record
      Item, Name, Examine, OfferType, GuidePrice: TBox;
    end;

    QuantityBounds: record
      Quantity: TBox;
      Spinner: TBoxArray;
      Buttons: TBoxArray;
    end;

    PriceBounds: record
      Price: TBox;
      Spinner: TBoxArray;
      Buttons: TBoxArray;
    end;

    CollectBounds: record
      Left, Right: TBox;
    end;

    TotalBounds: TBox;
    ConfirmButton, BackButton: TBox;
  end;

function TRSGrandExchangeOffer.IsOpen(): Boolean; forward;

(*
## GrandExchangeOffer.SetupInterface
```pascal
procedure TRSGrandExchangeOffer.SetupInterface();
```
Internal method used to setup the {ref}`TRSGrandExchangeOffer` coordinates.
This is automatically called for you on the {ref}`GrandExchangeOffer variable`.
*)
procedure TRSGrandExchangeOffer.SetupInterface();
begin
  Self.Bounds := GrandExchange.Bounds;

  Self.Title.Setup(Self.Bounds);
  Self.Title.IsOpen := @Self.IsOpen;

  with Self.Bounds do
  begin
    Self.InfoBounds.OfferType  := [X1+40, Y1+45, X1+130, Y1+61];
    Self.InfoBounds.Item       := [X1+64, Y1+71, X1+103, Y1+106];
    Self.InfoBounds.GuidePrice := [X1+40, Y1+114, X1+130, Y1+122];
    Self.InfoBounds.Name       := [X1+166, Y1+45, X2-20, Y1+61];
    Self.InfoBounds.Examine    := [X1+166, Y1+65, X2-20, Y1+128];

    Self.QuantityBounds.Quantity := [X1+39, Y1+158, X1+191, Y1+177];

    Self.TotalBounds   := [X1+36, Y2-91, X2-36, Y2-72];
    Self.ConfirmButton := [X1+166, Y2-54, X2-166, Y2-15];
    Self.BackButton    := [X1+17, Y2-46, X1+46, Y2-24];

    Self.CollectBounds.Left := [X2-105, Y2-51, X2-70, Y2-20];
    Self.CollectBounds.Right := [X2-54, Y2-51, X2-19, Y2-20];
  end;

  with Self.QuantityBounds.Quantity do
  begin
    Self.PriceBounds.Price := [X2+60, Y1, X2+253, Y2];

    Self.QuantityBounds.Spinner := [
      [X1-21, Y1+3, X1-9, Y2-4], [X2+9, Y1+3, X2+21, Y2-4]
    ];

    Self.QuantityBounds.Buttons := TBoxArray.Create([X1-23, Y2+4], 5, 1, 34, 24, [7,0]);
  end;

  with Self.PriceBounds.Price do
  begin
    Self.PriceBounds.Spinner := [
      [X1-21, Y1+3, X1-9, Y2-4], [X2+10, Y1+3, X2+22, Y2-4]
    ];

    Self.PriceBounds.Buttons := TBoxArray.Create([X1-23, Y2+4], 6, 1, 34, 24, [7,0]);
  end;
end;

(*
## GrandExchangeOffer.IsOpen
```pascal
function TRSGrandExchangeOffer.IsOpen(): Boolean;
```
Returns true if the Grand Exchange is open.

Example:
```pascal
WriteLn GrandExchangeOffer.IsOpen();
```
*)
function TRSGrandExchangeOffer.IsOpen(): Boolean;
begin
  if BankPin.IsOpen() then
  begin
    if not BankPin.WaitLoading(3000) then
      raise GetDebugLn('BankPin', 'GrandExchangeOffer pin buttons don''t seem to have loaded in 3 seconds.');

    if not BankPin.Enter(Profiles.GetPin()) then
      raise GetDebugLn('BankPin', 'Failed to enter GrandExchangeOffer pin.');
  end;

  Result := Self.Title.IsTitle('Grand Exchange:');
end;

(*
## GrandExchangeOffer.WaitOpen
```pascal
function TRSGrandExchangeOffer.WaitOpen(time: Integer; interval: Integer = -1): Boolean;
```
Returns true if the Grand Exchange is open within `time` milliseconds.

## Example:
```pascal
WriteLn GrandExchangeOffer.WaitOpen();
```
*)
function TRSGrandExchangeOffer.WaitOpen(time: Integer; interval: Integer = -1): Boolean;
begin
  if interval < 0 then interval := RandomMode(100, 50, 1500);
  Result := SleepUntil(Self.IsOpen(), interval, time);
end;

(*
## GrandExchangeOffer.Close
```pascal
function TRSGrandExchangeOffer.Close(escape: Boolean): Boolean;
function TRSGrandExchangeOffer.Close(escapeProbability: Single = 0): Boolean; overload;
```
Closes the GrandExchangeOffer, Depending on `escape` or `escapeProbability the function will
either click the button or press escape key.

Example:
```pascal
 WriteLn GrandExchangeOffer.Close();
```
*)
function TRSGrandExchangeOffer.Close(escape: Boolean): Boolean;
begin
  Result := Self.Title.Close(escape);
end;

function TRSGrandExchangeOffer.Close(escapeProbability: Single = -1): Boolean; overload;
begin
  Result := Self.Title.Close(escapeProbability);
end;

(*
## GrandExchangeOffer.Open
```pascal
function TRSGrandExchangeOffer.Open(): Boolean;
```
Attempts to open the {ref}`GrandExchangeOffer`, returns `True` on success.

Example:
```pascal
WriteLn GrandExchangeOffer.Open();
```
*)
function TRSGrandExchangeOffer.Open(): Boolean;
begin
  if Self.IsOpen() then Exit(True);
  if not GrandExchange.IsOpen() then Exit;

  //Mouse.Click(GrandExchange.OfferButton, EMouseButton.LEFT);
  Result := Self.WaitOpen(2000);
end;


(*
## GrandExchangeOffer.OfferInterface
```pascal
property TRSGrandExchangeOffer.OfferInterface: EGEOfferInterface;
```
Returns the {ref}`EGEOfferInterface` type of the {ref}`GrandExchangeOffer`
screen.

Example:
```pascal
WriteLn GrandExchangeOffer.OfferInterface;
```
*)
property TRSGrandExchangeOffer.OfferInterface: EGEOfferInterface;
begin
  if Self.Title.IsTitle('Grand Exchange: Offer status') then
    Exit(EGEOfferInterface.STATUS);
  Result := EGEOfferInterface.SETUP;
end;


(*
## GrandExchangeOffer.OfferType
```pascal
property TRSGrandExchangeOffer.OfferType: EGEOfferType;
```
Returns the {ref}`EGEOfferType` of the {ref}`GrandExchangeOffer` screen.

```{figure} ../../images/geo_type.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.OfferType;
```
*)
property TRSGrandExchangeOffer.OfferType: EGEOfferType;
begin
  if not Self.IsOpen() then Exit;
  if Target.HasColor(RSFonts.ORANGE, 0, 208, Self.InfoBounds.OfferType) then
    Exit(EGEOfferType.BUY);
  Result := EGEOfferType.SELL;
end;


(*
## GrandExchangeOffer.GuidePrice
```pascal
property TRSGrandExchangeOffer.GuidePrice: Integer;
```
Returns the guide price for the item in the {ref}`GrandExchangeOffer` if any.

```{figure} ../../images/geo_guide.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.GuidePrice;
```
*)
property TRSGrandExchangeOffer.GuidePrice: Integer;
var
  txt: String;
begin
  if not Self.IsOpen() then Exit;
  txt := OCR.Recognize(Self.InfoBounds.GuidePrice, RSFonts.PLAIN_11, [RSFonts.LIGHT_ORANGE], 0);
  Result := txt.ExtractInteger();
end;


(*
## GrandExchangeOffer.Item
```pascal
property TRSGrandExchangeOffer.Item: TRSItem;
```
Returns the item in the {ref}`GrandExchangeOffer` if any.

```{figure} ../../images/geo_item.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.Item;
```
*)
property TRSGrandExchangeOffer.Item: TRSItem;
begin
  if not Self.IsOpen() then Exit;
  Result := OCR.RecognizeStatic(Self.InfoBounds.Name, RSFonts.BOLD_SHADOW, [RSFonts.ORANGE], 0);
end;

(*
## GrandExchangeOffer.Examine
```pascal
property TRSGrandExchangeOffer.Examine: String;
```
Returns the examine text of the item in the {ref}`GrandExchangeOffer` if any.

```{figure} ../../images/geo_examine.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.Examine;
```
*)
property TRSGrandExchangeOffer.Examine: String;
var
  strs: TStringArray;
begin
  if not Self.IsOpen() then Exit;
  strs := OCR.RecognizeLines(Self.InfoBounds.Examine, RSFonts.PLAIN_11, [RSFonts.LIGHT_ORANGE], 0);

  if strs = [] then Exit;
  if strs.Last.EndsWith('%') then strs.Pop;
  Result := strs.Join(' ');
end;

(*
## GrandExchangeOffer.Fee
```pascal
property TRSGrandExchangeOffer.Fee: Double;
```
Returns the fee percentage of the the {ref}`GrandExchangeOffer` if any.

This is returned as a `Double`.

```{figure} ../../images/geo_fee.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.Fee;
```
*)
property TRSGrandExchangeOffer.Fee: Double;
var
  tpa: TPointArray;
  txt: String;
begin
  if not Self.IsOpen() then Exit;

  tpa := Target.FindColor(RSFonts.LIGHT_ORANGE, 0, Self.InfoBounds.Examine);
  if tpa = [] then Exit;
  tpa := tpa.Cluster(12, 8).Last;
  txt := OCR.Recognize(tpa.Bounds, RSFonts.PLAIN_11, [RSFonts.LIGHT_ORANGE], 0);
  Result := txt.ExtractInteger(0) / 100;
end;


(*
## GrandExchangeOffer.Quantity
```pascal
property TRSGrandExchangeOffer.Quantity: Integer;
property TRSGrandExchangeOffer.Quantity(value: Integer): Boolean;
```
Returns or sets the quantity of the the {ref}`GrandExchange` offer:
```{figure} ../../images/geo_quantity.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.Quantity;
WriteLn GrandExchangeOffer.Quantity[100];
WriteLn GrandExchangeOffer.Quantity;
```
*)
property TRSGrandExchangeOffer.Quantity: Integer;
var
  font: ^TPixelFont;
begin
  if not Self.IsOpen() then Exit(-1);

  case Self.OfferInterface of
    EGEOfferInterface.SETUP:  font := @RSFonts.PLAIN_11;
    EGEOfferInterface.STATUS: font := @RSFonts.BOLD_SHADOW;
  end;

  Result := OCR.RecognizeNumber(Self.QuantityBounds.Quantity, font^, [RSFonts.LIGHT_ORANGE], 0);
end;

property TRSGrandExchangeOffer.Quantity(value: Integer): Boolean;
var
  old: Integer;
begin
  old := Self.Quantity;

  case old of
    -1: Exit;
    value: Exit(True);
  end;

  if Self.OfferInterface <> EGEOfferInterface.SETUP then
    Exit;

  case value of
    old-1: Mouse.Click(Self.QuantityBounds.Spinner[EGEOfferSpinButton.DECREASE], EMouseButton.LEFT);
    old+1:
    begin
      if Biometrics.RandomBoolean() then
        Mouse.Click(Self.QuantityBounds.Spinner[EGEOfferSpinButton.INCREASE], EMouseButton.LEFT)
      else
        Mouse.Click(Self.QuantityBounds.Buttons[EGEOfferQuantity.ONE], EMouseButton.LEFT);
    end;

    old+10: Mouse.Click(Self.QuantityBounds.Buttons[EGEOfferQuantity.TEN], EMouseButton.LEFT);
    old+100: Mouse.Click(Self.QuantityBounds.Buttons[EGEOfferQuantity.HUNDRED], EMouseButton.LEFT);
    old+1000: Mouse.Click(Self.QuantityBounds.Buttons[EGEOfferQuantity.THOUSAND], EMouseButton.LEFT);
    else
    begin
      Mouse.Click(Self.QuantityBounds.Buttons[EGEOfferQuantity.CUSTOM], EMouseButton.LEFT);
      if not Chat.AnswerQuery('How many do you wish to buy?', ToStr(value), 2000) then
        Exit;
    end;
  end;

  Result := SleepUntil(Self.Quantity = value, 200, 2000);
end;


(*
## GrandExchangeOffer.CustomPricePercent
```pascal
property TRSGrandExchangeOffer.CustomPricePercent: Double;
property TRSGrandExchangeOffer.CustomPricePercent(value: Double): Boolean;
```
Returns or sets the custom price percentage button of the {ref}`GrandExchange`
offer:
```{figure} ../../images/geo_customperc.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.CustomPricePercent;
WriteLn GrandExchangeOffer.CustomPricePercent[0.12];
WriteLn GrandExchangeOffer.CustomPricePercent;
```
*)
property TRSGrandExchangeOffer.CustomPricePercent: Double;
var
  txt: String;
  value: Integer;
begin
  if not Self.IsOpen() then Exit(-1);
  if Self.OfferInterface <> EGEOfferInterface.SETUP then Exit(-1);

  txt := OCR.Recognize(Self.PriceBounds.Buttons[EGEOfferPrice.MINUS_CUSTOM], RSFonts.PLAIN_11, [RSFonts.ORANGE], 0);
  value := txt.After('-').ExtractInteger();

  if value > 0 then
    Result := value / 100;
end;

property TRSGrandExchangeOffer.CustomPricePercent(value: Double): Boolean;
var
  old: Double;
  str: String;
begin
  old := Self.CustomPricePercent;

  if old = -1 then Exit;

  //basically checking if old = value
  //ignoring extra decimals the user might have set
  if Abs(old - value) < 0.01 then Exit(True);

  if Biometrics.RandomBoolean() then
    Mouse.Move(Self.PriceBounds.Buttons[EGEOfferPrice.MINUS_CUSTOM])
  else
    Mouse.Move(Self.PriceBounds.Buttons[EGEOfferPrice.PLUS_CUSTOM]);

  if not ChooseOption.Select('Customise') then Exit;

  str := ToStr(Round(value*100));
  if not Chat.AnswerQuery('Set a percentage to', str, 2000) then
    Exit;

  Result := SleepUntil(Abs(Self.CustomPricePercent - value) < 0.01, 200, 2000);
end;


(*
## GrandExchangeOffer.Price
```pascal
property TRSGrandExchangeOffer.Price: Integer;
property TRSGrandExchangeOffer.Price(value: Integer): Boolean;
property TRSGrandExchangeOffer.Price(value: Integer; tolerance: Single): Boolean;
```
Returns or sets the price of the the {ref}`GrandExchange` offer:
```{figure} ../../images/geo_price.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.Price;
WriteLn GrandExchangeOffer.Price[1500500];
WriteLn GrandExchangeOffer.Price;
```
*)
property TRSGrandExchangeOffer.Price: Integer;
var
  font: ^TPixelFont;
begin
  if not Self.IsOpen() then Exit(-1);

  case Self.OfferInterface of
    EGEOfferInterface.SETUP:  font := @RSFonts.PLAIN_11;
    EGEOfferInterface.STATUS: font := @RSFonts.BOLD_SHADOW;
  end;

  Result := OCR.RecognizeNumber(Self.PriceBounds.Price, font^, [RSFonts.LIGHT_ORANGE], 0);
end;

property TRSGrandExchangeOffer.Price(value: Integer): Boolean;
var
  old: Integer;
  cpercent: Double;
begin
  old := Self.Price;

  case old of
    -1: Exit;
    value: Exit(True);
  end;

  if Self.OfferInterface <> EGEOfferInterface.SETUP then
    Exit;

  cpercent := Self.CustomPricePercent;

  case value of
    Self.GuidePrice: Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.GUIDE], EMouseButton.LEFT);
    Round(old*0.95): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.MINUS_FIVE], EMouseButton.LEFT);
    Round(old*1.05): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.PLUS_FIVE], EMouseButton.LEFT);
    Round(old*(1-cpercent)): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.MINUS_CUSTOM], EMouseButton.LEFT);
    Round(old*(1+cpercent)): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.PLUS_CUSTOM], EMouseButton.LEFT);
    else
    begin
      Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.CUSTOM], EMouseButton.LEFT);
      if not Chat.AnswerQuery('Set a price for each item:', ToStr(value), 2000) then
        Exit;
    end;
  end;

  Result := SleepUntil(Self.Price = value, 200, 2000);
end;

property TRSGrandExchangeOffer.Price(value: Integer; tolerance: Single): Boolean;
var
  old: Integer;
  cpercent: Double;
begin
  old := Self.Price;

  case old of
    -1: Exit;
    value: Exit(True);
  end;

  if Self.OfferInterface <> EGEOfferInterface.SETUP then
    Exit;

  cpercent := Self.CustomPricePercent;

  case value of
    Self.GuidePrice: Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.GUIDE], EMouseButton.LEFT);
    Round(old*0.95): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.MINUS_FIVE], EMouseButton.LEFT);
    Round(old*1.05): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.PLUS_FIVE], EMouseButton.LEFT);
    Round(old*(1-cpercent)): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.MINUS_CUSTOM], EMouseButton.LEFT);
    Round(old*(1+cpercent)): Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.PLUS_CUSTOM], EMouseButton.LEFT);
    else
    begin
      Mouse.Click(Self.PriceBounds.Buttons[EGEOfferPrice.CUSTOM], EMouseButton.LEFT);
      if not Chat.AnswerQuery('Set a price for each item:', ToStr(value), 2000) then
        Exit;
    end;
  end;

  Result := SleepUntil(InRange(Abs(Self.Price-value), value*(1-tolerance), value*(1+tolerance)), 200, 2000);
end;


(*
## GrandExchangeOffer.Total
```pascal
property TRSGrandExchangeOffer.Total: Integer;
```
Returns the total value of the the {ref}`GrandExchange` offer the user will
pay/receive:
```{figure} ../../images/geo_total.png
```

This is returned as an `Integer`.

Example:
```pascal
WriteLn GrandExchangeOffer.Total;
```
*)
property TRSGrandExchangeOffer.Total: Integer;
begin
  if not Self.IsOpen() then Exit;
  Result := OCR.RecognizeNumber(Self.TotalBounds, RSFonts.BOLD_SHADOW, [RSFonts.WHITE], 0);
end;

(*
## GrandExchangeOffer.TrueTotal
```pascal
property TRSGrandExchangeOffer.TrueTotal: Integer;
```
Returns the true total value of the the {ref}`GrandExchange` offer the user will
receive:
```{figure} ../../images/geo_truetotal.png
```

This is returned as an `Integer` and is only available in sell offers.

Example:
```pascal
WriteLn GrandExchangeOffer.TrueTotal;
```
*)
property TRSGrandExchangeOffer.TrueTotal: Integer;
var
  txt: String;
begin
  if not Self.IsOpen() then Exit;
  txt := OCR.Recognize(Self.TotalBounds, RSFonts.BOLD_SHADOW, [RSFonts.GREY], 0);
  Result := txt.Before('-').ExtractInteger();
end;



(*
## GrandExchangeOffer.Change
```pascal
function TRSGrandExchangeOffer.Change(item: TRSItem): Boolean;
```
Attempts to change the offer item to the specified `item`.

Example:
```pascal
WriteLn GrandExchangeOffer.Change('Abyssal whip');
```
*)
function TRSGrandExchangeOffer.Change(item: TRSItem): Boolean;
var
  slot: Integer;
begin
  if not Self.IsOpen() then Exit;
  if Self.Item.ToLower() = item.ToLower() then Exit(True);
  if Self.OfferInterface = EGEOfferInterface.STATUS then Exit;

  if Self.OfferType = EGEOfferType.SELL then
  begin
    if not Inventory.Items.Find(item, slot) then Exit;
    if not Inventory.Slots.Interact(slot, 'Offer') then Exit;
    Exit(SleepUntil(Self.Item.ToLower() = item.ToLower(), 200, 2000));
  end;

  if not GrandExchangeChat.IsOpen() then
  begin
    Mouse.Click(Self.InfoBounds.Item, EMouseButton.LEFT);
    if not GrandExchangeChat.WaitOpen(2000) then
      Exit;
  end;

  if not GrandExchangeChat.Contains(item) then
  begin
    if not GrandExchangeChat.SearchText[item] then
      Exit;
    if not GrandExchangeChat.ScrollTo(item) then
      Exit;
  end;

  if GrandExchangeChat.Click(item) then
    Result := SleepUntil(Self.Item.ToLower() = item.ToLower(), 200, 2000);
end;


(*
## GrandExchangeOffer.Confirm
```pascal
function TRSGrandExchangeOffer.Confirm(): Boolean;
```
Attempts to click the confirm button:

```{figure} ../../images/geo_confirm.png
```

Example:
```pascal
if GrandExchangeOffer.Change('Abyssal whip') then
  GrandExchangeOffer.Confirm();
```
*)
function TRSGrandExchangeOffer.Confirm(): Boolean;
begin
  if not Self.IsOpen() then Exit;
  if Self.OfferInterface <> EGEOfferInterface.SETUP then Exit;

  if not Target.HasColor(RSFonts.WHITE, 0, 1, Self.ConfirmButton) then
    Exit;

  Mouse.Click(Self.ConfirmButton, EMouseButton.LEFT);
  Result := GrandExchange.WaitOpen(2000);
end;

(*
## GrandExchangeOffer.Back
```pascal
function TRSGrandExchangeOffer.Back(): Boolean;
```
Attempts to click the back button:

```{figure} ../../images/geo_back.png
```

Example:
```pascal
GrandExchangeOffer.Back();
```
*)
function TRSGrandExchangeOffer.Back(): Boolean;
begin
  if not Self.IsOpen() then Exit;
  Mouse.Click(Self.BackButton, EMouseButton.LEFT);
  Result := GrandExchange.WaitOpen(2000);
end;


(*
## GrandExchangeOffer.HasItems
```pascal
function TRSGrandExchangeOffer.HasItems(): Boolean;
```
Checks if we have items to collect in an offer:

```{figure} ../../images/geo_collect.png
```

Example:
```pascal
WriteLn GrandExchangeOffer.HasItems();
```
*)
function TRSGrandExchangeOffer.HasItems(): Boolean;
begin
  if not Self.IsOpen() then Exit;
  if Self.OfferInterface <> EGEOfferInterface.STATUS then Exit;

  Result := Target.HasColor(ITEM_BORDER, 0, 1, Self.CollectBounds.Left) or
            Target.HasColor(ITEM_BORDER, 0, 1, Self.CollectBounds.Right);
end;


(*
## GrandExchangeOffer.Collect
```pascal
function TRSGrandExchangeOffer.Collect(): Boolean;
```
Attempts to collect items in an offer.

Example:
```pascal
GrandExchangeOffer.Collect();
```
*)
function TRSGrandExchangeOffer.Collect(): Boolean;
begin
  if not Self.IsOpen() then Exit;
  if Self.OfferInterface <> EGEOfferInterface.STATUS then Exit;

  if not Self.HasItems() then Exit(True);

  if Target.HasColor(ITEM_BORDER, 0, 1, Self.CollectBounds.Left) then
    Mouse.Click(Self.CollectBounds.Left, EMouseButton.LEFT);

  if Target.HasColor(ITEM_BORDER, 0, 1, Self.CollectBounds.Right) then
    Mouse.Click(Self.CollectBounds.Right, EMouseButton.LEFT);

  Result := SleepUntil(not Self.HasItems(), 200, 2000);
end;

(*
## GrandExchangeOffer.CreateBuyOffer
```pascal
function TRSGrandExchangeOffer.CreateBuyOffer(itemName : String; price, quantity : Integer = -1) : Boolean;
```

Attempts to create a buy offer for the specified `itemName`, `price` and `quantity`.

Example:
```pascal
if GrandExchangeOffer.CreateBuyOffer('Abyssal whip', 1500000, 1) then
  WriteLn('Buy offer created successfully!');
```
*)
function TRSGrandExchangeOffer.CreateBuyOffer(itemName: String; price, quantity: Integer = -1) : Boolean;
begin
  if not Self.IsOpen() then Exit;
  if (Self.OfferInterface <> EGEOfferInterface.SETUP) or (Self.OfferType <> EGEOfferType.BUY) then Exit;

  if not GrandExchangeChat.WaitOpen(2000, 250) then Exit;
  if not GrandExchangeChat.SearchText[itemName] then Exit;
  if not GrandExchangeChat.Click(itemName) then Exit;
  if not SleepUntil(Self.Item.ToLower() = itemName.ToLower(), 250, 2000) then Exit;

  if (quantity <> -1) and not Self.Quantity[quantity] then Exit;
  if (price <> -1) and not Self.Price[price] then Exit;

  Result := Self.Confirm();
end;

(*
## GrandExchangeOffer.CreateSellOffer
```pascal
function TRSGrandExchangeOffer.CreateSellOffer(itemName : String; price, quantity : Integer = -1) : Boolean;
```

Attempts to create a sell offer for the specified `itemName`, `price` and `quantity`.

Example:
```pascal
if GrandExchangeOffer.CreateSellOffer('Abyssal whip', 1500000, 1) then
  WriteLn('Sell offer created successfully!');
```
*)
function TRSGrandExchangeOffer.CreateSellOffer(itemName: String; price, quantity: Integer = -1) : Boolean;
begin
  if not Self.IsOpen() then Exit;
  if (Self.OfferInterface <> EGEOfferInterface.SETUP) or (Self.OfferType <> EGEOfferType.SELL) then Exit;

  if not Inventory.Items.Click(itemName) then Exit;
  if not SleepUntil(Self.Item.ToLower() = itemName.ToLower(), 250, 2000) then Exit;

  if (quantity <> -1) and not Self.Quantity[quantity] then Exit;
  if (price <> -1) and not Self.Price[price] then Exit;

  Result := Self.Confirm();
end;

var
(*
## GrandExchangeOffer variable
Global {ref}`TRSGrandExchangeOffer` variable.
*)
  GrandExchangeOffer: TRSGrandExchangeOffer;
