(*
# Gear Layouts
The `Gear Layouts` is a handler for OSRS gear.
"Gear" refers to as any equippable item in the game.
*)

{$DEFINE WL_GEAR_LAYOUTS_INCLUDED}
{$INCLUDE_ONCE WaspLib/osrs.simba}

type
(*
## TRSGear
Type that represents a gear layout.
*)
  TRSGear = record
    Name: String;
    Items: array [ERSEquipment] of TRSItem;
    SpecLevel: Integer;
  end;

function TRSGear.ToJSON(): TJSONItem;
var
  slot: ERSEquipment;
begin
  Result := NewJSONObject();

  for slot := Low(ERSEquipment) to High(ERSEquipment) do
    Result.AddString(ToStr(slot).After('ERSEquipment.').ToLower(), Self.Items[slot]);

  Result.AddInt('spec_level', Self.SpecLevel);
end;

type
(*
## TRSGearLayouts
Main type for the {ref}`Gear Layouts`.
*)
  TRSGearLayouts = record
    Sets: array of TRSGear;
    Config: TConfigJSON;
  end;

procedure TRSGearLayouts.Setup();
var
  i: Integer;
  slot: ERSEquipment;
begin
  Self.Config.Setup('gearlayouts' + PATH_SEP + ToStr(PlayerIndex));
  SetLength(Self.Sets, Self.Config.Count);

  try
    for i := 0 to Self.Config.Count-1 do
    begin
      Self.Sets[i].Name := Self.Config.Key[i];

      for slot := Low(ERSEquipment) to High(ERSEquipment) do
        Self.Sets[i].Items[slot] := Self.Config.Item[i].Item.Item[Ord(slot)].AsString;

      Self.Sets[i].SpecLevel := Self.Config.Item[i].Item.Item[Ord(High(ERSEquipment))+1].AsInt;
    end;
  except
    FileDelete(Self.Config.Path); //corrupted config.
  end;
end;

procedure TRSGearLayouts.UpdateConfig();
var
  i: Integer;
  slot: ERSEquipment;
begin
  for i := 0 to High(Self.Sets) do
  begin
    Self.Config.Key[i] := Self.Sets[i].Name;

    for slot := Low(ERSEquipment) to High(ERSEquipment) do
      Self.Config.Item[i].Item.Item[Ord(slot)].AsString := Self.Sets[i].Items[slot];

    Self.Config.Item[i].Item.Item[Ord(High(ERSEquipment))+1].AsInt := Self.Sets[i].SpecLevel;
  end;

  Self.Config.SaveConfig();
end;

procedure TRSGearLayouts.AddLayout(gear: TRSGear);
begin
  Self.Sets += gear;
  Self.Config.Add(gear.Name, gear.ToJSON());
  Self.UpdateConfig();
end;

procedure TRSGearLayouts.RemoveLayout(index: Integer);
begin
  Delete(Self.Sets, index, 1);
  Self.Config.Delete(index);
  Self.UpdateConfig();
end;

var
  GearLayouts: TRSGearLayouts;
